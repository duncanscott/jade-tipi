plugins {
    id 'java'
    id 'groovy'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.hidetake.ssh' version '2.12.0'
}

apply from: "${rootProject.projectDir}/gradle/scripts/spring-boot.gradle"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

sourceSets {
    main {
        groovy {
            if (!project.hasProperty('db') || project.property('db') != 'foundationdb') {
                exclude '**/config/FoundationDBConfig.groovy'
            }
        }
    }
}

tasks.withType(GroovyCompile) {
    options.compilerArgs << '-parameters'
}

ext {
    set('springCloudVersion', "2025.0.0")
}

dependencies {
    implementation 'org.apache.groovy:groovy'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation project(':backend:libraries:jade-tipi-id')

    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    if (project.hasProperty('db') && project.property('db') == 'foundationdb') {
        implementation 'org.foundationdb:fdb-java:7.1.59'
    } else {
        implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
    }

    //implementation 'org.springframework.cloud:spring-cloud-starter-consul-discovery'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation group: 'org.spockframework', name: 'spock-core', version: spockVersion
    testImplementation group: 'org.spockframework', name: 'spock-spring', version: spockVersion

    runtimeOnly group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: logstashLogbackEncoderVersion
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
